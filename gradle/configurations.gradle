apply from: rootProject.file('common.gradle')

ext.buildConfigurations = []

@SuppressWarnings('GrMethodMayBeStatic')
def buildConfigNames() {
  buildConfigurations.findAll {
    it.os == osIdentifier() && it.arch == archIdentifier()
  }.collect { it.name }.toSet()
}

@SuppressWarnings('GrMethodMayBeStatic')
def buildConfigs() {
  return buildConfigurations.findAll {
    it.os == osIdentifier() && it.arch == archIdentifier()
  }
}

def locate(def file) {
  if (os().isLinux()) {
    try {
      def locateCommand = "locate ${file}"
      def process = locateCommand.execute()
      process.waitFor()

      if (process.exitValue() == 0) {
        return process.in.text.trim()
      }
    } catch (Exception e) {
      logger.warn("Exception when locating ${file}: ${e.message}")
    }
  }
  return null
}

def locateLibrary(String libName) {
  if (os().isLinux()) {
    try {
      def locateCommand = "gcc -print-file-name=${libName}.so"
      def process = locateCommand.execute()
      process.waitFor()

      if (process.exitValue() == 0) {
        def output = process.in.text.trim()
        if (output.endsWith("${libName}.so")) {
          return output
        }
      }
    } catch (Exception e) {
      logger.warn("Exception when locating ${libName} library: ${e.message}")
    }
  }
  return null
}

def locateLibasan() {
  return locateLibrary('libasan')
}

def locateLibtsan() {
  return locateLibrary('libtsan')
}

def checkFuzzerSupport() {
  // libFuzzer requires clang - check if clang supports -fsanitize=fuzzer
  try {
    def testFile = File.createTempFile("fuzzer_check", ".cpp")
    testFile.text = "extern \"C\" int LLVMFuzzerTestOneInput(const char*, long) { return 0; }"
    testFile.deleteOnExit()

    def process = ["clang++", "-fsanitize=fuzzer", "-c", testFile.absolutePath, "-o", "/dev/null"].execute()
    process.waitFor()
    testFile.delete()

    return process.exitValue() == 0
  } catch (Exception e) {
    logger.debug("Exception when checking fuzzer support: ${e.message}")
    return false
  }
}

ext.libasan = locateLibasan()
ext.libtsan = locateLibtsan()
ext.hasFuzzerSupport = checkFuzzerSupport()

if (ext.libasan == null) {
  logger.debug("Asan library not found")
}
if (ext.libtsan == null) {
  logger.debug("Tsan library not found")
}
if (!ext.hasFuzzerSupport) {
  logger.debug("libFuzzer not available (requires clang with -fsanitize=fuzzer support)")
}

def hasAsan() {
  return isMusl() ? false : ext.libasan != null
}

def hasTsan() {
  return isMusl() ? false : ext.libtsan != null
}

def hasFuzzer() {
  return isMusl() ? false : ext.hasFuzzerSupport
}

def isActive(def name) {
  def methodName = "has${name.capitalize()}"
  if (this.metaClass.respondsTo(this, methodName)) {
    this."${methodName}"()
  } else {
    // no activation method defined; default to 'true'
    true
  }
}

ext.addBuildConfiguration = { String name, String os, String arch, List<String> compilerArgs, List<String> linkerArgs, Map<String, String> testEnv = [:] ->
  buildConfigurations << [active: isActive(name), name: name, os: os, arch: arch, compilerArgs: compilerArgs, linkerArgs: linkerArgs, testEnv: testEnv]
}

ext {
  hasAsan = this.&hasAsan
  hasTsan = this.&hasTsan
  hasFuzzer = this.&hasFuzzer
  buildConfigNames = this.&buildConfigNames
  buildConfigs = this.&buildConfigs
  locateLibasan = this.&locateLibasan
  locateLibtsan = this.&locateLibtsan
}

// make sure the provided version is correctly propagated to the build
version = project.findProperty("ddprof_version") ?: version

// ======= Define build configurations below ========

def commonLinuxCompilerArgs = [
  "-fPIC",
  "-fno-omit-frame-pointer",
  "-momit-leaf-frame-pointer",
  "-fvisibility=hidden",
  "-fdata-sections",
  "-ffunction-sections",
  "-std=c++17",
  "-DPROFILER_VERSION=\"${version}\"",
  "-DCOUNTERS"
]

def commonLinuxLinkerArgs = ["-ldl", "-Wl,-z,defs", "--verbose", "-lpthread", "-lm", "-lrt", "-v", "-Wl,--build-id"]

def commonMacosCompilerArgs = commonLinuxCompilerArgs + ["-D_XOPEN_SOURCE", "-D_DARWIN_C_SOURCE"]

def asanEnv = hasAsan() ?
  ['LD_PRELOAD': libasan,
    // warning: stack use after return can cause slowness on arm64
    "ASAN_OPTIONS" : "allocator_may_return_null=1:unwind_abort_on_malloc=1:use_sigaltstack=0:detect_stack_use_after_return=0:handle_segv=1:halt_on_error=0:abort_on_error=0:print_stacktrace=1:symbolize=1:suppressions=${rootDir}/gradle/sanitizers/asan.supp",
    "UBSAN_OPTIONS" : "halt_on_error=0:abort_on_error=0:print_stacktrace=1:suppressions=${rootDir}/gradle/sanitizers/ubsan.supp",
    // lsan still does not run for all tests - manually trigger on some tests
    "LSAN_OPTIONS" :  "detect_leaks=0"
  ] : [:]

def asanCompilerArgs = hasAsan() ? [
  '-g',
  // Generate debug information
  '-DDEBUG',
  // Enable debug mode
  '-fPIC',
  // Position-independent code
  '-fsanitize=address',
  // AddressSanitizer for memory errors
  '-fsanitize=undefined',
  // UndefinedBehaviorSanitizer for undefined behavior
  '-fno-sanitize-recover=all',
  // Disable recovery to ensure program halts on error
  '-fsanitize=float-divide-by-zero',
  // Catch floating point division by zero
  '-fstack-protector-all',
  // Protect against stack buffer overflows
  '-fsanitize=leak',
  // LeakSanitizer to detect memory leaks
  '-fsanitize=pointer-overflow',
  // Catch pointer overflows
  '-fsanitize=return',
  // Check for uninitialized return values
  '-fsanitize=bounds',
  // BoundsSanitizer for array bounds
  '-fsanitize=alignment',
  // Catch misaligned pointer access
  '-fsanitize=object-size',
  // Catch accesses that are likely out of bounds
  '-fno-omit-frame-pointer',
  // Keep frame pointer for better debugging
  '-fno-optimize-sibling-calls'  // Disable tail call optimization for better stack traces
] : []

def asanLinkerArgs = hasAsan() ? [
  "-L${file(libasan).parent}",
  "-lasan",
  "-lubsan",
  "-fsanitize=address",
  "-fsanitize=undefined",
  "-fno-omit-frame-pointer"  // Often recommended with sanitizers
] : []

def tsanCompilerArgs = hasTsan() ? [
  '-g',
  // Generate debug information
  '-DDEBUG',
  // Enable debug mode
  '-fPIC',
  // Position-independent code
  '-fsanitize=thread',
  // ThreadSanitizer for
  '-fno-omit-frame-pointer',
  // Keep frame pointer for better debugging
  '-fno-optimize-sibling-calls'  // Disable tail call optimization for better stack traces
] : []

def tsanLinkerArgs = hasTsan() ?  [
  "-L${file(libtsan).parent}",
  "-ltsan",
  "-fsanitize=thread",
  "-fno-omit-frame-pointer"  // Often recommended with sanitizers
] : []

def tsanEnv = hasTsan() ?
  ['LD_PRELOAD': libtsan,
    "TSAN_OPTIONS" : "suppressions=${rootDir}/gradle/sanitizers/tsan.supp"
  ] : [:]

// Linux
addBuildConfiguration 'release', 'linux', 'x64',
  ['-O3', '-DNDEBUG', '-g'] + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + [
    "-Wl,-z,nodelete",
    "-static-libstdc++",
    "-static-libgcc",
    "-Wl,--exclude-libs,ALL",
    "-Wl,--gc-sections"
  ]
addBuildConfiguration 'debug', 'linux', 'x64',
  ['-O0', '-g', '-DDEBUG'] + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs

addBuildConfiguration 'asan', 'linux', 'x64',
  asanCompilerArgs + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + asanLinkerArgs,
  asanEnv

addBuildConfiguration 'tsan', 'linux', 'x64',
  tsanCompilerArgs + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + tsanLinkerArgs,
  tsanEnv

addBuildConfiguration 'release', 'linux', 'arm64',
  ['-O3', '-DNDEBUG', '-g'] + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + [
    "-Wl,-z,nodelete",
    "-static-libstdc++",
    "-static-libgcc",
    "-Wl,--exclude-libs,ALL",
    "-Wl,--gc-sections"
  ]
addBuildConfiguration 'debug', 'linux', 'arm64',
  ['-O0', '-g', '-DDEBUG'] + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs

addBuildConfiguration 'asan', 'linux', 'arm64',
  asanCompilerArgs + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + asanLinkerArgs,
  asanEnv

addBuildConfiguration 'tsan', 'linux', 'arm64',
  tsanCompilerArgs + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + tsanLinkerArgs,
  tsanEnv

// Fuzzer configuration (requires clang with libFuzzer support)
// Note: Fuzzers are compiled separately and linked as executables, not shared libraries.
// The fuzzer build config provides common compiler flags; actual fuzzer linking happens
// in the fuzz subproject with -fsanitize=fuzzer added at link time.
def fuzzerCompilerArgs = hasFuzzer() ? [
  '-g',
  // Generate debug information for crash analysis
  '-DDEBUG',
  // Enable debug mode and assertions
  '-fPIC',
  // Position-independent code
  '-fsanitize=address',
  // AddressSanitizer for memory errors
  '-fsanitize=undefined',
  // UndefinedBehaviorSanitizer for undefined behavior
  '-fno-sanitize-recover=all',
  // Halt on sanitizer errors for reliable crash detection
  '-fno-omit-frame-pointer',
  // Keep frame pointer for stack traces
  '-fno-optimize-sibling-calls'
  // Disable tail call optimization for better stack traces
] : []

def fuzzerLinkerArgs = hasFuzzer() ? [
  "-fsanitize=address",
  "-fsanitize=undefined",
  "-fno-omit-frame-pointer"
] : []

def fuzzerEnv = hasFuzzer() ?
  ["ASAN_OPTIONS" : "allocator_may_return_null=1:detect_stack_use_after_return=0:handle_segv=0:abort_on_error=1:symbolize=1:suppressions=${rootDir}/gradle/sanitizers/asan.supp",
   "UBSAN_OPTIONS" : "halt_on_error=1:abort_on_error=1:print_stacktrace=1:suppressions=${rootDir}/gradle/sanitizers/ubsan.supp"
  ] : [:]

addBuildConfiguration 'fuzzer', 'linux', 'x64',
  fuzzerCompilerArgs + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + fuzzerLinkerArgs,
  fuzzerEnv

addBuildConfiguration 'fuzzer', 'linux', 'arm64',
  fuzzerCompilerArgs + commonLinuxCompilerArgs,
  commonLinuxLinkerArgs + fuzzerLinkerArgs,
  fuzzerEnv

// MacOS
addBuildConfiguration 'release', 'macos', 'arm64',
  commonMacosCompilerArgs + ['-O3', '-DNDEBUG'],
  []
addBuildConfiguration 'debug', 'macos', 'arm64',
  commonMacosCompilerArgs + ['-O0', '-g', '-DDEBUG'],
  []

// macOS fuzzer configuration
def fuzzerMacosCompilerArgs = hasFuzzer() ? [
  '-g', '-DDEBUG', '-fPIC',
  '-fsanitize=address', '-fsanitize=undefined',
  '-fno-sanitize-recover=all', '-fno-omit-frame-pointer',
  '-fno-optimize-sibling-calls'
] : []

def fuzzerMacosLinkerArgs = hasFuzzer() ? [
  "-fsanitize=address", "-fsanitize=undefined"
] : []

def fuzzerMacosEnv = hasFuzzer() ?
  ["ASAN_OPTIONS" : "allocator_may_return_null=1:detect_stack_use_after_return=0:abort_on_error=1:symbolize=1",
   "UBSAN_OPTIONS" : "halt_on_error=1:abort_on_error=1:print_stacktrace=1"
  ] : [:]

addBuildConfiguration 'fuzzer', 'macos', 'arm64',
  fuzzerMacosCompilerArgs + commonMacosCompilerArgs,
  fuzzerMacosLinkerArgs,
  fuzzerMacosEnv