plugins {
  id 'java'
  id 'me.champeau.jmh' version '0.7.1'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation project(path: ":ddprof-lib", configuration: 'debug')
  implementation project(path: ":ddprof-test-tracer")
  implementation 'org.openjdk.jmh:jmh-core:1.36'
  implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
}

sourceSets {
  jmh {
    java {
      runtimeClasspath += project(':ddprof-lib').sourceSets.main.output
    }
  }
}

jmh {
  def javaTestHome = System.getenv("JAVA_TEST_HOME")
  def javaHome = javaTestHome != null ? javaTestHome : System.getenv("JAVA_HOME")

  // Set the JVM executable - this is used by the JMH plugin to fork benchmark processes
  jvm = "${javaHome}/bin/java"

  // Explicitly set fork to use external JVM (not Gradle's JVM)
  fork = 3

  iterations = 3
  timeOnIteration = '3s'
  warmup = '1s'
  warmupIterations = 3
}

// Configure all JMH-related JavaExec tasks to use the correct JDK
tasks.withType(JavaExec).matching { it.name.startsWith('jmh') }.configureEach {
  def javaTestHome = System.getenv("JAVA_TEST_HOME")
  def javaHome = javaTestHome != null ? javaTestHome : System.getenv("JAVA_HOME")

  executable = "${javaHome}/bin/java"
}

jmhJar {
  manifest {
    attributes(
      'Main-Class': 'com.datadoghq.profiler.stresstest.Main'
      )
  }
  archiveFileName = "stresstests.jar"
}

task runStressTests(type: Exec) {
  dependsOn jmhJar
  def javaHome = System.getenv("JAVA_TEST_HOME")
  if (javaHome == null) {
    javaHome = System.getenv("JAVA_HOME")
  }
  group = 'Execution'
  description = 'Run JMH stresstests'
  commandLine "${javaHome}/bin/java", '-jar', 'build/libs/stresstests.jar', '-prof', 'com.datadoghq.profiler.stresstest.WhiteboxProfiler', 'counters.*'
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs.addAll(['--release', '8'])
}